apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'

buildscript {
    ext.android_buildtools_version = '35.0.0'
    ext.androidx_version = '1.0.0'
    ext.jackson_version = '2.8.6'
    ext.jpa_version = '2.1.1'
    ext.kotlin_version = '2.1.10'
    ext.rxjava2_version = '2.2.3'
    ext.reactor_version = '3.2.2.RELEASE'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.9.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:2.0.0"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

idea {
    project {
        jdkName = '1.8'
    }
}

group = 'io.requery'
version = '1.6.1'
description = 'A light but powerful object mapper and SQL generator for Java/Android'

Properties properties = new Properties()
File localProperties = project.rootProject.file('local.properties')
if (localProperties.exists()) {
    properties.load(localProperties.newDataInputStream())
}

configure([project(':requery'), project(':requery-processor'), project(':requery-kotlin'), project(':requery-jackson')]) {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    javadoc.options.links("http://docs.oracle.com/javase/8/docs/api/");

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set('sources')
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                groupId rootProject.group
                artifactId project.name
                version rootProject.version

                artifact sourcesJar
                artifact javadocJar

                pom.withXml {
                    asNode().children().last() + project.pomXml
                }
            }
        }
    }
}

configure([project(':requery'), project(':requery-processor'), project(':requery-android'), project(':requery-kotlin'), project(':requery-jackson')]) {

    ext {
        pomXml = {
            resolveStrategy = Closure.DELEGATE_FIRST
            name project.name
            description project.description ?: rootProject.description
            url 'https://github.com/requery/requery'
            scm {
                url 'https://github.com/requery/requery.git'
                connection 'scm:git:git://github.com/requery/requery.git'
                developerConnection 'scm:git:git@github.com/requery/requery.git'
            }
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/license/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    id 'npurushe'
                    name 'Nikhil Purushe'
                }
            }
        }
    }
}
